buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "1.12.2-1.0"
group = "net.msrandom.simplyhorses"
archivesBaseName = "SimplyHorses"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

def intGeneticsVersions = "1.0.7-6"

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20180814"
}

repositories {
    maven {
        name = "Ashley's repo"
        url = "https://nexus.msrandom.net/repository/maven-releases"
    }
}

dependencies {
    compile(group: "net.msrandom.genetics", name: "IntegerGenetics", version: intGeneticsVersions)
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

shadowJar {
    dependencies {
        include(dependency(group: "net.msrandom.genetics", name: "IntegerGenetics", version: intGeneticsVersions))
    }
}

artifacts {
    archives shadowJar
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

tasks.reobfShadowJar.mustRunAfter shadowJar
tasks.build.dependsOn shadowJar
